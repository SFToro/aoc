set dotenv-load := true 
dev day:
    cargo watch -q -c -w ./day-{{day}}/src -x "test -p day-{{day}} -q"

# Hiding command names with @
create day:
    @cp -r ./starter ./day-{{day}}
    @sed -i "s/day-/day-{{day}}/" ./day-{{day}}/Cargo.toml
    @curl -s https://adventofcode.com/2023/day/{{day}}/input --cookie "session=$AOC_COOKIE" > ./day-{{day}}/input.txt
    @echo "Created day-{{day}}"

solve day part:
    cargo run -p day-{{day}} --release --bin part-{{part}}

clippy day:
    cargo clippy -p day-{{day}} --fix --allow-dirty
